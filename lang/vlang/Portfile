# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compiler_blacklist_versions 1.0
PortGroup           github 1.0
PortGroup           legacysupport 1.1
PortGroup           makefile 1.0

# clock_gettime
legacysupport.newest_darwin_requires_legacy 15
# link legacysupport statically for compilers
legacysupport.use_static  yes

github.setup        vlang v weekly.2022.13
github.tarball_from archive
revision            0

name                vlang
# Remove weekly from version number
version             [string range ${version} 7 end]

description         Simple, fast, safe, compiled language for developing maintainable software
long_description    {*}${description}. Compiles itself in <1s with zero library dependencies. This port \
                    provides weekly builds of Vlang.

license             MIT
categories          lang
maintainers         {harens @harens} openmaintainer

# Install the V compiler as a separate distfile.
# Based on https://github.com/macports/macports-ports/blob/master/games/minetest/Portfile
set vc_commit       de63146da0a0f93628f8a65e70267e2c8d4c7ce1
set main_distfile   ${distfiles}
set vc_distfile     ${vc_commit}${extract.suffix}
set vc_mastersite   https://github.com/vlang/vc/archive

distfiles           ${main_distfile}:main \
                    ${vc_distfile}:vc

master_sites        ${github.master_sites}:main \
                    ${vc_mastersite}:vc

checksums           ${main_distfile} \
                    rmd160  22933b810c1445643d18430c99803a0210be2e19 \
                    sha256  5df66008b01de13d02da9f8f0ed47438e1ce01e82543738a33a7dcc65240aa37 \
                    size    4078460 \
                    ${vc_distfile} \
                    rmd160  2dc5fc11de6b9b265095d26e6cc08f4a5b5c5c73 \
                    sha256  db89d158a721ef1f37be2865f273e9a017e5f1c69be0ed60830a4c625e73b0b2 \
                    size    1353099

# https://trac.macports.org/ticket/64913
if {${build_arch} in "ppc ppc64"} {
    configure.ldflags-append -latomic
}

# Path to V compiler
set vc_path ${workpath}/vc-${vc_commit}

# ./thirdparty/stdatomic/nix/atomic.h:12:10: fatal error: 'stdatomic.h' file not found
# Based on cmus port: compatible compilers are Xcode > Xcode 7, clang > 3.7, and gcc > 4.8
compiler.c_standard 2011
compiler.blacklist-append {clang < 700} macports-clang-3.*

post-patch {
    # https://github.com/vlang/v/issues/10796 - Set correct deployment target.
    foreach f [list ${worksrcpath}/vlib/v/builder/cc.v ${vc_path}/v_win.c ${vc_path}/v.c] {
        reinplace "s|-mmacosx-version-min=10.7|-mmacosx-version-min=${macosx_deployment_target}|" ${f}
    }

    # Disable vlang self update feature.
    copy -force ${filespath}/vup.v ${worksrcpath}/cmd/tools
}

# -cg and -showcc allows for better error debugging
# local prevents fetching the V compiler during the build
build.args-append   local=true \
                    VFLAGS="-v -cg -showcc -prod -cc ${configure.cc} -cflags [shellescape ${configure.cflags}]" \
                    VC=${vc_path}

destroot {
    set library_path ${destroot}${prefix}/lib/${name}
    set examples_path ${destroot}${prefix}/share/examples

    xinstall -d ${examples_path}
    move ${worksrcpath}/examples ${examples_path}/${name}

    xinstall -d ${library_path}
    foreach f {cmd thirdparty v v.mod vlib} {
        move ${worksrcpath}/${f} ${library_path}/${f}
    }

    ln -s ${prefix}/lib/${name}/v ${destroot}${prefix}/bin

    # Vlang compiles the subcommands in the tools dir on first runtime usage (e.g. v doctor).
    # Allow running Vlang to those in the _developer group, rather than running sudo each time.
    # See https://github.com/vlang/v/issues/10324

    # Majority of subcommands only require ${library_path}/cmd/tools to be writable but v self requires
    # all of ${library_path}

    fs-traverse item ${library_path} {
        file attributes ${item} -group _developer -permissions g+w
    }
}

# Based on various Android ports - don't make files world-writable.
# e.g. https://github.com/macports/macports-ports/blob/master/java/android/Portfile
notes "
The Vlang library directory is group _developer writable. You need to be a member of the
_developer group to use Vlang. If you are not, run:

sudo dscl . append /Groups/_developer GroupMembership <username>
"
