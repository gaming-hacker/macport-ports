# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           compiler_blacklist_versions 1.0
PortGroup           github 1.0
PortGroup           legacysupport 1.1
PortGroup           makefile 1.0

# clock_gettime
legacysupport.newest_darwin_requires_legacy 15

github.setup        vlang v weekly.2022.16
github.tarball_from archive
revision            1

name                vlang
# Remove weekly from version number
version             [string range ${version} 7 end]
homepage            https://vlang.io/

description         Simple, fast, safe, compiled language for developing maintainable software
long_description    {*}${description}. Compiles itself in <1s with zero library dependencies. This port \
                    provides weekly builds of Vlang.

license             MIT
categories          lang
maintainers         {harens @harens} openmaintainer

# Install the V compiler as a separate distfile.
# Based on https://github.com/macports/macports-ports/blob/master/games/minetest/Portfile
set vc_commit       4505557b56638eb931182f764580767e22cf110d
set main_distfile   ${distfiles}
set vc_distfile     ${vc_commit}${extract.suffix}
set vc_mastersite   https://github.com/vlang/vc/archive

distfiles           ${main_distfile}:main \
                    ${vc_distfile}:vc

master_sites        ${github.master_sites}:main \
                    ${vc_mastersite}:vc

checksums           ${main_distfile} \
                    rmd160  76d3912525735076f2c498e2361dd9fa3c52a95e \
                    sha256  d6088e945a2d55f87263bf83ff422494d2f598f6c92f405a54307587a25befab \
                    size    4172510 \
                    ${vc_distfile} \
                    rmd160  59cd41e6a47f2182cadd8eea17ce17c5cd5671db \
                    sha256  f610ca3c078286e8287e691a91b1c8ad42337a880bf9626cafdbfe2d7abe1e7b \
                    size    1364231

# Optional runtime dependency to use the net.http and net.websocket modules
depends_run         path:lib/libssl.dylib:openssl

# https://trac.macports.org/ticket/64913
if {${build_arch} in "ppc ppc64"} {
    configure.ldflags-append -latomic
}

# Path to V compiler
set vc_path ${workpath}/vc-${vc_commit}

# ./thirdparty/stdatomic/nix/atomic.h:12:10: fatal error: 'stdatomic.h' file not found
# Based on cmus port: compatible compilers are Xcode > Xcode 7, clang > 3.7, and gcc > 4.8
compiler.c_standard 2011
compiler.blacklist-append {clang < 700} macports-clang-3.*

# Stop V from looking in Homebrew directories, and remove hardcoded MacPorts prefix.
# V has built-in font rendering, but users can optionally use freetype.
patchfiles          patch-freetype2-prefix.diff \
                    patch-openssl-prefix.diff

# Use LegacySupport with the time module
# See https://github.com/vlang/v/issues/6605
if { ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
    patchfiles-append patch-clock-gettime.diff
}

post-patch {
    # https://github.com/vlang/v/issues/10796 - Set correct deployment target.
    foreach f [list ${worksrcpath}/vlib/v/builder/cc.v ${vc_path}/v.c] {
        reinplace "s|-mmacosx-version-min=10.7|-mmacosx-version-min=${macosx_deployment_target}|" ${f}
    }

    # Disable vlang self update feature.
    copy -force ${filespath}/vup.v ${worksrcpath}/cmd/tools

    foreach f [list vlib/net/openssl/c.v vlib/fontstash/a_d_use_freetype.v] {
        reinplace -W ${worksrcpath} "s|@PREFIX@|${prefix}|g" ${f}
    }

    if { ${os.major} <= [option legacysupport.newest_darwin_requires_legacy] } {
        reinplace "s|@PREFIX@|${prefix}|g" ${worksrcpath}/vlib/time/time_nix.c.v
    }
}

# -cg, -showcc and -show-c-output allows for better error debugging
# local prevents fetching the V compiler during the build
build.args-append   local=true \
                    VFLAGS="-v -cg -showcc -show-c-output -prod -cc ${configure.cc} -cflags [shellescape ${configure.cflags}]" \
                    VC=${vc_path}

# See https://trac.macports.org/ticket/64913
# In case _developer group doesn't exist
add_users vlang group=_developer

destroot {
    set library_path ${destroot}${prefix}/lib/${name}
    set examples_path ${destroot}${prefix}/share/examples

    xinstall -d ${examples_path}
    move ${worksrcpath}/examples ${examples_path}/${name}

    xinstall -d ${library_path}
    foreach f {cmd thirdparty v v.mod vlib} {
        move ${worksrcpath}/${f} ${library_path}/${f}
    }

    ln -s ${prefix}/lib/${name}/v ${destroot}${prefix}/bin

    # Vlang compiles the subcommands in the tools dir on first runtime usage (e.g. v doctor).
    # Allow running Vlang to those in the _developer group, rather than running sudo each time.
    # See https://github.com/vlang/v/issues/10324

    # Majority of subcommands only require ${library_path}/cmd/tools to be writable but v self requires
    # all of ${library_path}

    fs-traverse item ${library_path} {
        file attributes ${item} -group _developer -permissions g+w
    }
}

# Delete leftover subcommands compiled during runtime
post-deactivate {
    delete ${prefix}/lib/${name}
}

# Based on various Android ports - don't make files world-writable.
# e.g. https://github.com/macports/macports-ports/blob/master/java/android/Portfile
notes "
The Vlang library directory is group _developer writable. You need to be a member of the
_developer group to use Vlang. If you are not, run:

sudo dscl . append /Groups/_developer GroupMembership <username>
"
